Package sc;

//##############################################################\\
                             Helpers 

lettre         = [['a' .. 'z'] + ['A' .. 'Z']] ;
chiffre        = ['0' .. '9']                  ;
alpha_sign     = (lettre | '_' | '$')          ;
alpha_sign_num = ( alpha_sign | chiffre )      ;

//##############################################################\\
                             Tokens
/******* Numbers *************/
nb = chiffre+;

/****** Punctuation **********/
po        = '(' ;
pf        = ')' ;
co        = '[' ;
cf        = ']' ;
ao        = '{' ;
af        = '}' ;
comma     = ',' ;
semicolon = ';' ;

/****** Mathematical Operators *******/
plus   = '+' ;
minus  = '-' ;
mult   = '*' ;
div    = '/' ;

/****** Logical Operators **********/
equal      = '=' ;
not        = '!' ;
and        = '&' ;
or         = '|' ;
lower_than = '<' ;

/****** Key words of L **********/
var_type = 'entier' ;
if       = 'si'     ;
then     = 'alors'  ;
else     = 'sinon'  ;
while    = 'tantque';
do       = 'faire'  ;
read     = 'lire'   ;
write    = 'ecrire' ;
return   = 'retour' ;

/********** ID ******************/
id = alpha_sign (alpha_sign_num)*; 

/****** Ignored signs **********/
espaces     = (' ' | 13 | 10)+ ;
commentaire = '#' [[0 .. 0xffff] - [10 + 13]]* (10 | 13 | 10 13);

//##############################################################\\
                         Ignored Tokens 

espaces, commentaire;

//##############################################################\\
                          Productions 

/*********************** Program ********************************/
programme =
 {prog} ldv_optional ldf ;

/*********************** Functions ****************************/
ldf =
 {regular} df ldf | {eps};

df =
 {regular} id po ldv pf ldv_optional bi;

/********************** Variables *******************************/
ldv_optional =
 {regular} ldv semicolon | {eps};

ldv =
 {regular} dv ldv_bis | {eps};

ldv_bis =
 {regular} comma dv ldv_bis | {eps};

dv =
 {regular_variable} var_type id |
 {regular_array} var_type id co nb cf;

/********************* Instructions ***************************/
bi =
 {regular} ao li af ;

li =
 {regular} i i_bis;

i_bis =
 {regular} i i_bis | {eps} ;

i =
 {affect} var equal e semicolon |
 {if} if e then bi else_block |
 {while} while e do bi |
 {return} return e semicolon |
 {func_call} id po le pf semicolon |
 {write} write po e pf semicolon;

var_i =
    {regular} id |
    {array} id co nb cf;

else_block =
 {regular} else bi | {eps};

/********************* Expressions ***************************/
e =
 {regular} e or and_e |
 {pass} and_e;

and_e =
 {regular} and_e and comp_e |
 {pass} comp_e;

comp_e =
 {equal} comp_e equal arith_low_e |
 {lower_than} comp_e lower_than arith_low_e |
 {pass} arith_low_e;

arith_low_e =
 {plus} arith_low_e plus arith_high_e |
 {moins} arith_low_e minus arith_high_e |
 {pass} arith_high_e;

arith_high_e =
 {mult} arith_high_e mult neg_e |
 {div} arith_high_e div neg_e |
 {pass} neg_e;

neg_e =
 {regular} not parenth_e |
 {pass} parenth_e;

parenth_e =
 {regular} po e pf |
 {pass} element;
 

element =
 {number} nb |
 {variable} var |
 {read} read po pf |
 {func_call} app_function;

var =
 {regular} id |
 {array} id co e cf;

/*
element =
 {number} nb |
 {variable} id |
 {array} id co e cf |
 {read} read po pf |
 {func_call} app_function;
 */
app_function =
 {regular} id po le pf;

le =
 {regular} e e_bis | {eps};

e_bis =
 {regular} comma e e_bis | {eps};
